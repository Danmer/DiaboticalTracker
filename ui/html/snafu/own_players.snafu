BEGIN TEMPLATE own_players
    SETTING iterator own_team_players.count
    SETTING item_direction direction
    SETTING cell_direction "vertical"
    SETTING align pivot
    SETTING item_spacing 1
    SETTING cell_spacing 0

    DOUBLE nWidth
    INT sL

    # ========================================
    # SVGS
    STRING square
    square = "<svg width='1' height='1'><rect width='1' height='1' x='0' y='0' fill='white' /></svg>"

    STRING hp_bar
    hp_bar = "<svg width='" & height & "'"
    hp_bar = hp_bar & " height='" & 0.68 & "'>"
    hp_bar = hp_bar & "<rect width='" & height & "'"
    hp_bar = hp_bar & " height='" & 0.68 & "'"
    hp_bar = hp_bar & " x='0' y='0' fill='white' /></svg>"

    STRING armor_bar
    armor_bar = "<svg width='" & height & "'"
    armor_bar = armor_bar & " height='" & 0.46 & "'>"
    armor_bar = armor_bar & "<rect width='" & height & "'"
    armor_bar = armor_bar & " height='" & 0.46 & "'"
    armor_bar = armor_bar & " x='0' y='0' fill='white' /></svg>"

    STRING solo_hp_bar
    solo_hp_bar = "<svg width='" & nWidth & "'"
    solo_hp_bar = solo_hp_bar & " height='" & 1.6 & "'>"
    solo_hp_bar = solo_hp_bar & "<rect width='" & nWidth & "'"
    solo_hp_bar = solo_hp_bar & " height='" & 1.6 & "'"
    solo_hp_bar = solo_hp_bar & " x='0' y='0' fill='white' /></svg>"

    STRING solo_armor_bar
    solo_armor_bar = "<svg width='" & nWidth & "'"
    solo_armor_bar = solo_armor_bar & " height='" & 0.4 & "'>"
    solo_armor_bar = solo_armor_bar & "<rect width='" & nWidth & "'"
    solo_armor_bar = solo_armor_bar & " height='" & 0.4 & "'"
    solo_armor_bar = solo_armor_bar & " x='0' y='0' fill='white' /></svg>"

    # ========================================
    # COMMON
    DOUBLE dead_font_size
    dead_font_size = height * 0.65

    DOUBLE ready_size
    ready_size = height * 0.2

    DOUBLE half_ready_size
    half_ready_size = ready_size / 2

    DOUBLE ready_offset_x
    ready_offset_x = height - ready_size

    DOUBLE name_offset_x
    name_offset_x = height + 0.2

    DOUBLE name_size
    name_size = height / 4

    DOUBLE hp_bar_offset_x
    hp_bar_offset_x = height + 0.3
    hp_bar_offset_x = hp_bar_offset_x + name_size

    DOUBLE armor_bar_offset_x
    armor_bar_offset_x = hp_bar_offset_x + 0.7

    DOUBLE item_image_size
    item_image_size = height * 0.8

    DOUBLE item_offset_x
    item_offset_x = height + 3

    DOUBLE small_item_size
    small_item_size = height / 2

    DOUBLE small_item_image_size
    small_item_image_size = small_item_size - 0.2

    DOUBLE lives_offset
    lives_offset = height + name_size
    lives_offset = lives_offset + 1.8

    DOUBLE lives_offset_text
    lives_offset_text = height + name_size
    lives_offset_text = lives_offset_text + 2

    DOUBLE lives_image_size
    lives_image_size = small_item_size * 0.7

    DOUBLE solo_lives_image_size
    solo_lives_image_size = small_item_size * 0.9

    INT dir
    IF direction == "horizontal reverse"
        dir = 1
    END IF

    INT j

    IF game_data.team_size > 1
        # ========================================
        # AVATAR
        COL_OUTPUT j image_width height
        COL_OUTPUT j image_height height
        COL_OUTPUT j width height
        COL_OUTPUT j height height
        j = j + 1

        # ========================================
        # DEAD INDICATOR
        COL_OUTPUT j height height
        COL_OUTPUT j width height
        COL_OUTPUT j x 0
        COL_OUTPUT j fill "#FF000033"
        COL_OUTPUT j value "X"
        COL_OUTPUT j font "montserrat-bold"
        COL_OUTPUT j font_size dead_font_size
        COL_OUTPUT j align "center"
        COL_OUTPUT j color "#ff0000"
        j = j + 1

        # ========================================
        # SELF INDICATOR
        COL_OUTPUT j image_width DOUBLE (height * 0.5)
        COL_OUTPUT j image_height DOUBLE (height * 0.5)
        COL_OUTPUT j height height
        COL_OUTPUT j x 0
        j = j + 1

        # ========================================
        # READY INDICATOR
        COL_OUTPUT j image square
        COL_OUTPUT j image_width ready_size
        COL_OUTPUT j image_height ready_size
        COL_OUTPUT j width ready_size
        COL_OUTPUT j height ready_size
        COL_OUTPUT j x ready_offset_x
        COL_OUTPUT j offset_x ready_offset_x
        j = j + 1

        # ========================================
        # PLAYER NAME
        COL_OUTPUT j font "roboto-bold"
        COL_OUTPUT j font_size name_size
        COL_OUTPUT j shadow 1
        COL_OUTPUT j height height
        COL_OUTPUT j width name_size
        COL_OUTPUT j x name_offset_x
        COL_OUTPUT j align "center"
        j = j + 1

        # ========================================
        # HEALTH BAR
        COL_OUTPUT j width 0.68
        COL_OUTPUT j height height
        COL_OUTPUT j fill "#00000080"
        COL_OUTPUT j image hp_bar
        COL_OUTPUT j image_width height
        COL_OUTPUT j image_height 0.68
        COL_OUTPUT j x hp_bar_offset_x
        j = j + 1

        # ========================================
        # ARMOR BAR
        COL_OUTPUT j width 0.46
        COL_OUTPUT j height height
        COL_OUTPUT j fill "#00000080"
        COL_OUTPUT j image armor_bar
        COL_OUTPUT j image_color "#ffffff"
        COL_OUTPUT j image_width height
        COL_OUTPUT j image_height 0.46
        COL_OUTPUT j x armor_bar_offset_x
        j = j + 1

        # ========================================
        # RESPAWN TIMER
        COL_OUTPUT j width 2.2
        COL_OUTPUT j height height
        COL_OUTPUT j fill "#17171788"
        COL_OUTPUT j font "roboto-bold"
        COL_OUTPUT j shadow 1
        COL_OUTPUT j font_size 1.5
        COL_OUTPUT j x hp_bar_offset_x
        j = j + 1

    ELSE
        # team_size <= 1

        DOUBLE name_indent
        name_indent = height / 3
        
        # ========================================
        # NAME
        COL_OUTPUT j height nWidth
        COL_OUTPUT j width height
        COL_OUTPUT j font font
        COL_OUTPUT j color "#ffffff"
        COL_OUTPUT j align "left-edge"
        COL_OUTPUT j indent name_indent
        COL_OUTPUT j font_size fontSize
        IF dir
            COL_OUTPUT j align "right-edge"
        ELSE
            COL_OUTPUT j align "left-edge"
        END IF
        j = j + 1

        # ========================================
        # AVATAR
        COL_OUTPUT j width height
        COL_OUTPUT j height height
        COL_OUTPUT j fill "#111111"
        COL_OUTPUT j image_width height
        COL_OUTPUT j image_height height
        COL_OUTPUT j x 0
        IF dir
            COL_OUTPUT j y DOUBLE (height * -1)
        ELSE
            COL_OUTPUT j y nWidth
        END IF
        j = j + 1

        # ========================================
        # DEAD INDICATOR
        COL_OUTPUT j height height
        COL_OUTPUT j width height
        COL_OUTPUT j x 0
        COL_OUTPUT j y nWidth
        COL_OUTPUT j fill "#FF000033"
        COL_OUTPUT j value "X"
        COL_OUTPUT j font "montserrat-bold"
        COL_OUTPUT j font_size dead_font_size
        COL_OUTPUT j align "center"
        COL_OUTPUT j color "#ff0000"
        IF dir
            COL_OUTPUT j y DOUBLE (height * -1)
        ELSE
            COL_OUTPUT j y nWidth
        END IF
        j = j + 1

        # ========================================
        # READY INDICATOR
        DOUBLE ready_size
        ready_size = height * 0.2

        DOUBLE half_ready_size
        half_ready_size = ready_size / 2

        DOUBLE ready_offset_x
        ready_offset_x = height - ready_size

        COL_OUTPUT j image square
        COL_OUTPUT j image_width ready_size
        COL_OUTPUT j image_height ready_size
        COL_OUTPUT j width ready_size
        COL_OUTPUT j height ready_size
        COL_OUTPUT j x ready_offset_x
        IF dir
            COL_OUTPUT j y DOUBLE (ready_size * -1)
        ELSE
            COL_OUTPUT j offset_x DOUBLE (nWidth + ready_offset_x)
        END IF
        j = j + 1

        # ========================================
        # SPECTATOR HP BAR
        COL_OUTPUT j width 1.6
        COL_OUTPUT j height nWidth
        COL_OUTPUT j image solo_hp_bar
        COL_OUTPUT j image_width nWidth
        COL_OUTPUT j image_height 1.6
        COL_OUTPUT j fill "#00000066"
        COL_OUTPUT j x DOUBLE (height + 0.4)
        j = j + 1

        # ========================================
        # SPECTATOR ARMOR BAR
        DOUBLE solo_armor_offset
        solo_armor_offset = height + 2.1

        COL_OUTPUT j width 0.4
        COL_OUTPUT j height nWidth
        COL_OUTPUT j fill "#00ff00"
        COL_OUTPUT j image solo_armor_bar
        COL_OUTPUT j image_color "#ffffff"
        COL_OUTPUT j image_width nWidth
        COL_OUTPUT j image_height 0.4
        COL_OUTPUT j fill "#00000066"
        COL_OUTPUT j x solo_armor_offset
        j = j + 1
        
    END IF


ON_UPDATE

    j = 0

    STRING avatar_url
    INT avatar_length
    avatar_length = LENGTH own_team_players[IDX].avatar
    IF avatar_length > 0
        avatar_url = "customization/avatar/" & own_team_players[IDX].avatar
        avatar_url = avatar_url & ".png"
    ELSE 
        avatar_url = "customization/avatar/av_no_avatar.png"
    END IF

    STRING ready_color
    IF own_team_players[IDX].ready
        ready_color = "#00ff00"
    ELSE 
        ready_color = "#ff0000"
    END IF

    DOUBLE health_clip
    DOUBLE solo_health_clip
    health_clip = -1 * DOUBLE(own_team_players[IDX].hp_percentage / 100)
    solo_health_clip = health_clip

    DOUBLE armor_clip
    DOUBLE solo_armor_clip
    armor_clip = -1 * DOUBLE(own_team_players[IDX].armor_percentage / 100)
    solo_armor_clip = armor_clip

    IF !dir
        solo_health_clip = solo_health_clip * -1
        solo_armor_clip = solo_armor_clip * -1
    END IF


    IF game_data.team_size > 1

        # ========================================
        # AVATAR
        OUTPUT IDX j image avatar_url
        OUTPUT IDX j fill "#111111"
        IF !own_team_players[IDX].alive
            OUTPUT IDX j GRAYSCALE 1
        END IF
        j = j + 1

        # ========================================
        # DEAD INDICATOR
        IF own_team_players[IDX].alive
            OUTPUT IDX j opacity 0
        END IF
        j = j + 1

        # ========================================
        # SELF INDICATOR
        IF own_team_players[IDX].is_self
            OUTPUT IDX j image "images/icons/fa/caret-down.svg"
        END IF
        j = j + 1

        # ========================================
        # READY INDICATOR
        OUTPUT IDX j image_color ready_color
        IF game_data.game_stage != 1
            OUTPUT IDX j opacity 0            
        END IF
        j = j + 1

        # ========================================
        # PLAYER NAME
        STRING player_name
        player_name = SUBSTR own_team_players[IDX].name 0 8
        OUTPUT IDX j value player_name
        IF !own_team_players[IDX].alive
            OUTPUT IDX j opacity 0.6
        END IF
        j = j + 1

        # ========================================
        # HEALTH BAR
        OUTPUT IDX j clip_y health_clip
        OUTPUT IDX j image_color game_data.own_team.color
        IF !own_team_players[IDX].alive
            OUTPUT IDX j opacity 0
        END IF
        j = j + 1

        # ========================================
        # ARMOR BAR
        OUTPUT IDX j clip_y armor_clip
        IF !own_team_players[IDX].alive
            OUTPUT IDX j opacity 0
        END IF
        j = j + 1

        # ========================================
        # RESPAWN TIMER
        IF game_data.show_respawn_timers
            OUTPUT IDX j value own_team_players[IDX].respawn_timer
            IF own_team_players[IDX].alive
                OUTPUT IDX j opacity 0
            END IF
        ELSE
            OUTPUT IDX j opacity 0
        END IF
        j = j + 1

        # ========================================
        # LIVES
        INT show_lives
        show_lives = 0

        INT lives_mode
        lives_mode = 0
        IF IN_EDITOR
            lives_mode = 1
        END IF
        IF game_data.game_mode == "ca" || game_data.game_mode == "extinction"
            lives_mode = 1
        END IF
        IF game_data.game_mode == "rocket_arena" || game_data.game_mode == "shaft_arena"
            lives_mode = 1
        END IF
        IF game_data.game_mode == "bounty"
            lives_mode = 1
        END IF

        IF lives_mode && sL

            show_lives = 1
            life_count = own_team_players[IDX].life_count & "x"

            # ========================================
            # ARENA LIFE COUNT
            OUTPUT IDX j width small_item_size
            OUTPUT IDX j height small_item_size
            OUTPUT IDX j x lives_offset_text
            OUTPUT IDX j shadow 1
            OUTPUT IDX j font_size 1.8
            OUTPUT IDX j font "roboto-bold"
            OUTPUT IDX j value life_count
            IF own_team_players[IDX].life_count < 1
                OUTPUT IDX j opacity 0
            END IF
            j = j + 1

            # ========================================
            # ARENA LIFE 2
            OUTPUT IDX j image "images/icons/fa/heart.svg"
            OUTPUT IDX j width small_item_size
            OUTPUT IDX j height small_item_size
            OUTPUT IDX j image_width lives_image_size
            OUTPUT IDX j image_height lives_image_size
            OUTPUT IDX j x lives_offset
            OUTPUT IDX j offset_x small_item_size
            OUTPUT IDX j image_color game_data.own_team.color
            IF own_team_players[IDX].life_count < 1
                OUTPUT IDX j opacity 0
            END IF
            j = j + 1

            OUTPUT IDX j image "images/icons/icon_dead.svg"
            OUTPUT IDX j width DOUBLE(height * 0.7)
            OUTPUT IDX j height height
            OUTPUT IDX j image_width DOUBLE(item_image_size * 0.66)
            OUTPUT IDX j image_height DOUBLE(item_image_size * 0.66)
            OUTPUT IDX j x DOUBLE(lives_offset - 0.4)
            OUTPUT IDX j image_color "#ffffff"
            OUTPUT IDX j opacity 0
            IF own_team_players[IDX].life_count < 1
                OUTPUT IDX j opacity 0.5
            END IF
            j = j + 1

            ## ========================================
            ## ARENA LIFE 2
            #OUTPUT IDX j image_color game_data.own_team.color
            #OUTPUT IDX j image "images/icons/fa/heart.svg"
            #OUTPUT IDX j width small_item_size
            #OUTPUT IDX j height small_item_size
            #OUTPUT IDX j image_width lives_image_size
            #OUTPUT IDX j image_height lives_image_size
            #OUTPUT IDX j x lives_offset
            #IF own_team_players[IDX].life_count < 1
            #    OUTPUT IDX j opacity 0
            #END IF
            #j = j + 1
#
            ## ========================================
            ## ARENA LIFE 2
            #OUTPUT IDX j image "images/icons/fa/heart.svg"
            #OUTPUT IDX j width small_item_size
            #OUTPUT IDX j height small_item_size
            #OUTPUT IDX j image_width lives_image_size
            #OUTPUT IDX j image_height lives_image_size
            #OUTPUT IDX j x lives_offset
            #OUTPUT IDX j offset_x small_item_size
            #OUTPUT IDX j image_color game_data.own_team.color
            #IF own_team_players[IDX].life_count < 2
            #    OUTPUT IDX j opacity 0
            #END IF
            #j = j + 1

        END IF

        # ========================================
        # POWERUP / ITEM COMMON
        DOUBLE item_offset_x_extra
        item_offset_x_extra = item_offset_x

        IF show_lives == 1
            item_offset_x_extra = item_offset_x + small_item_size
        END IF

        INT items
        items = 0

        IF own_team_players[IDX].has_macguffin
            items = items + 1
        END IF
        IF own_team_players[IDX].has_flag
            items = items + 1
        END IF
        IF own_team_players[IDX].has_powerup
            items = items + 1
        END IF

        # ========================================
        # MACGUFFIN
        IF own_team_players[IDX].has_macguffin
            OUTPUT IDX j width height
            OUTPUT IDX j height height
            OUTPUT IDX j image "images/item_macguffin.svg"
            OUTPUT IDX j image_color "#f8d206"
            OUTPUT IDX j image_width item_image_size
            OUTPUT IDX j image_height item_image_size
            OUTPUT IDX j x item_offset_x_extra

            IF items > 1
                OUTPUT IDX j width small_item_size
                OUTPUT IDX j height small_item_size
                OUTPUT IDX j image_width small_item_image_size
                OUTPUT IDX j image_height small_item_image_size
            END IF

            IF game_data.game_mode == "extinction" || game_data.game_mode == "bounty"
                OUTPUT IDX j opacity 0
            END IF

            j = j + 1
        END IF

        # ========================================
        # FLAG
        IF own_team_players[IDX].has_flag
            OUTPUT IDX j width height
            OUTPUT IDX j height height
            OUTPUT IDX j image "images/item_flag.svg"
            OUTPUT IDX j hue own_team_players[IDX].flag_color
            OUTPUT IDX j image_width item_image_size
            OUTPUT IDX j image_height item_image_size
            OUTPUT IDX j x item_offset_x_extra

            IF items > 1
                OUTPUT IDX j width small_item_size
                OUTPUT IDX j height small_item_size
                OUTPUT IDX j image_width small_item_image_size
                OUTPUT IDX j image_height small_item_image_size
            END IF

            j = j + 1
        END IF

        # ========================================
        # POWERUP
        DOUBLE powerup_offset_y
        IF own_team_players[IDX].has_powerup
            OUTPUT IDX j width height
            OUTPUT IDX j height height
            OUTPUT IDX j image own_team_players[IDX].pd_powerup_image
            OUTPUT IDX j image_color own_team_players[IDX].pd_powerup_color
            OUTPUT IDX j image_width item_image_size
            OUTPUT IDX j image_height item_image_size
            OUTPUT IDX j x item_offset_x_extra

            IF items > 1
                OUTPUT IDX j width small_item_size
                OUTPUT IDX j height small_item_size
                OUTPUT IDX j image_width small_item_image_size
                OUTPUT IDX j image_height small_item_image_size
                OUTPUT IDX j offset_x small_item_size
            END IF

            j = j + 1
        END IF

    ELSE
        # team_size <= 1

        STRING name_bg
        name_bg = game_data.own_team.color & name_bg_opacity

        DOUBLE name_indent
        name_indent = height / 3

        STRING name_color_grayscale 
        name_color_grayscale = GRAYSCALE game_data.own_team.color

        STRING name_color
        name_color = "#ffffff"

        IF name_color_grayscale > 0.55
            name_color = "#1c1c1c"
        END IF
        
        # ========================================
        # NAME
        OUTPUT IDX j value own_team_players[IDX].name
        OUTPUT IDX j fill name_bg
        OUTPUT IDX j color name_color
        j = j + 1

        # ========================================
        # AVATAR
        OUTPUT IDX j image avatar_url
        IF !own_team_players[IDX].alive
            OUTPUT IDX j GRAYSCALE 1
        END IF
        j = j + 1

        # ========================================
        # DEAD INDICATOR
        IF own_team_players[IDX].alive
            OUTPUT IDX j opacity 0
        END IF
        j = j + 1

        # ========================================
        # READY INDICATOR
        OUTPUT IDX j image_color ready_color
        IF game_data.game_stage != 1
            OUTPUT IDX j opacity 0
        END IF
        j = j + 1


        # ========================================
        # SPECTATOR HP BAR
        IF game_data.game_mode == "duel" && game_data.spectator
            OUTPUT IDX j image_color game_data.own_team.color
            OUTPUT IDX j clip_y solo_health_clip
        ELSE
            OUTPUT IDX j opacity 0
        END IF
        j = j + 1

        # ========================================
        # SPECTATOR ARMOR BAR
        IF game_data.game_mode == "duel" && game_data.spectator
            OUTPUT IDX j clip_y solo_armor_clip
        ELSE
            OUTPUT IDX j opacity 0
        END IF
        j = j + 1
        

        # ========================================
        # SOLO LIVES

        DOUBLE solo_life_offset_x
        solo_life_offset_x = solo_armor_offset + 0.6
        IF game_data.spectator
            solo_life_offset_x = solo_armor_offset + 0.6
        ELSE
            solo_life_offset_x = height + 0.4
        END IF

        DOUBLE solo_main_life_offset_y
        solo_main_life_offset_y = nWidth - small_item_size
        IF dir
            solo_main_life_offset_y = 0
        END IF

        lives_mode = 0
        IF game_data.game_mode == "ca"
            lives_mode = 1
        END IF
        IF game_data.game_mode == "rocket_arena" || game_data.game_mode == "shaft_arena"
            lives_mode = 1
        END IF

        IF lives_mode && sL

            # ========================================
            # ARENA LIFE 2
            OUTPUT IDX j image_color game_data.own_team.color
            OUTPUT IDX j image "images/icons/fa/heart.svg"
            OUTPUT IDX j width small_item_size
            OUTPUT IDX j height small_item_size
            OUTPUT IDX j image_width solo_lives_image_size
            OUTPUT IDX j image_height solo_lives_image_size
            OUTPUT IDX j x solo_life_offset_x
            OUTPUT IDX j y solo_main_life_offset_y
            IF own_team_players[IDX].life_count < 1
                OUTPUT IDX j opacity 0
            END IF
            j = j + 1

            # ========================================
            # ARENA LIFE 2
            IF dir
                solo_main_life_offset_y = solo_main_life_offset_y + DOUBLE (solo_lives_image_size + 0.4)
            ELSE
                solo_main_life_offset_y = solo_main_life_offset_y - DOUBLE (solo_lives_image_size + 0.4)
            END IF
            OUTPUT IDX j image_color game_data.own_team.color
            OUTPUT IDX j image "images/icons/fa/heart.svg"
            OUTPUT IDX j width small_item_size
            OUTPUT IDX j height small_item_size
            OUTPUT IDX j image_width solo_lives_image_size
            OUTPUT IDX j image_height solo_lives_image_size
            OUTPUT IDX j x solo_life_offset_x
            OUTPUT IDX j y solo_main_life_offset_y
            IF own_team_players[IDX].life_count < 2
                OUTPUT IDX j opacity 0
            END IF
            j = j + 1

        END IF

        IF game_data.game_mode == "duel" && sL
            # ========================================
            # MAIN LIFE

            OUTPUT IDX j width small_item_size
            OUTPUT IDX j height small_item_size
            OUTPUT IDX j image_width solo_lives_image_size
            OUTPUT IDX j image_height solo_lives_image_size
            OUTPUT IDX j x solo_life_offset_x
            OUTPUT IDX j y solo_main_life_offset_y

            IF game_data.is_golden_frag
                OUTPUT IDX j image "images/diabotical_o.svg"
                OUTPUT IDX j image_color game_data.own_team.color
            ELSE
                OUTPUT IDX j image "images/diabotical_o_shield.svg"
                OUTPUT IDX j hue game_data.own_team.color_dark
            END IF
            j = j + 1

            # ========================================
            # DUEL LIVES
            INT lives
            IF game_data.own_team.team_score > game_data.enemy_team.team_score
                lives = game_data.own_team.team_score - game_data.enemy_team.team_score
            ELSE 
                lives = 0
            END IF
            
            INT current_life
            current_life = 0

            DOUBLE current_life_offset_y
            IF dir
                current_life_offset_y = small_item_size + 0.2
            ELSE
                current_life_offset_y = nWidth - small_item_size
                current_life_offset_y = current_life_offset_y - DOUBLE (small_item_size + 0.2)
            END IF

            IF lives > 5

                OUTPUT IDX j width small_item_size
                OUTPUT IDX j height small_item_size
                OUTPUT IDX j image_width solo_lives_image_size
                OUTPUT IDX j image_height solo_lives_image_size
                OUTPUT IDX j image "images/icons/fa/heart.svg"
                OUTPUT IDX j image_color game_data.own_team.color
                OUTPUT IDX j x solo_life_offset_x
                OUTPUT IDX j y current_life_offset_y
                j = j + 1

                DOUBLE live_count_offset_y
                IF dir
                    live_count_offset_y = small_item_size * 2
                    live_count_offset_y = live_count_offset_y + 0.6
                    OUTPUT IDX j align "left-edge"
                ELSE
                    OUTPUT IDX j align "right-edge"
                    live_count_offset_y = 0 - DOUBLE(small_item_size * 2)
                    live_count_offset_y = live_count_offset_y - 0.6
                END IF
                STRING live_count_string
                live_count_string = lives & "x"

                OUTPUT IDX j x solo_life_offset_x
                OUTPUT IDX j y live_count_offset_y
                OUTPUT IDX j width small_item_size
                
                OUTPUT IDX j font "roboto-bold"
                OUTPUT IDX j font_size DOUBLE (solo_lives_image_size / 1.5)
                OUTPUT IDX j color "#ffffff"
                OUTPUT IDX j shadow 1
                OUTPUT IDX j value live_count_string
                j = j + 1

            ELSE 
                FOR lives

                    current_life = current_life + 1

                    OUTPUT IDX j width small_item_size
                    OUTPUT IDX j height small_item_size
                    OUTPUT IDX j image_width solo_lives_image_size
                    OUTPUT IDX j image_height solo_lives_image_size
                    OUTPUT IDX j image "images/icons/fa/heart.svg"
                    OUTPUT IDX j image_color game_data.own_team.color
                    OUTPUT IDX j x solo_life_offset_x
                    OUTPUT IDX j y current_life_offset_y

                    IF dir
                        current_life_offset_y = current_life_offset_y + DOUBLE (small_item_size + 0.2)
                    ELSE
                        current_life_offset_y = current_life_offset_y - DOUBLE (small_item_size + 0.2)
                    END IF
                    j = j + 1

                END FOR
            END IF
        END IF

    END IF

END TEMPLATE