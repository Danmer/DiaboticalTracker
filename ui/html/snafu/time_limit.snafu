BEGIN TEMPLATE time_limit
    SETTING item_direction "vertical"
    SETTING cell_direction "horizontal"
    SETTING align pivot
    SETTING item_spacing 0
    SETTING cell_spacing 0
    SETTING row_align "center"
    INT shadow

    COL_OUTPUT 0 font font
    COL_OUTPUT 0 align pivot
    COL_OUTPUT 0 color color
    COL_OUTPUT 0 height fontSize
    IF shadow
        COL_OUTPUT 0 shadow 1
    END IF

    COL_OUTPUT 1 font font
    COL_OUTPUT 1 align pivot
    COL_OUTPUT 1 color color
    COL_OUTPUT 1 height fontSize
    IF shadow
        COL_OUTPUT 1 shadow 1
    END IF

    STRING game_state
    STRING state_value
    INT game_state_length
    INT limit_overtime

ON_UPDATE

    game_state = ""
    state_value = ""

    IF game_data.warmup
        game_state = "ingame_message_warmup"
    ELSE
        IF game_data.round_mode            
            game_state = "ingame_message_round"
            state_value = game_data.round + 1
        ELSE
            limit_overtime = game_data.time_limit + game_data.overtime_seconds
            limit_overtime = limit_overtime + game_data.tide_time_offset

            IF game_data.overtime_seconds
                game_state = "ingame_message_overtime_time"
                state_value = TIMESTAMP limit_overtime
            ELSE

                IF game_data.time_limit > 0

                    IF game_data.is_golden_frag
                        game_state = "ingame_message_golden_frag"
                    END IF

                    IF game_data.in_overtime_frag_mode 
                        game_state = "ingame_message_overtime_fraglimit"
                        state_value = game_data.dynamic_overtime_frag_limit 
                    END IF

                    game_state_length = LENGTH game_state
                    IF game_state_length == 0
                        state_value = TIMESTAMP limit_overtime
                    END IF

                END IF

            END IF

        END IF
    END IF

    game_state_length = LENGTH game_state
    IF game_state_length > 0
        OUTPUT 0 0 LVALUE game_state
    ELSE
        OUTPUT 0 0 LVALUE "empty"
    END IF

    OUTPUT 0 0 VALUE state_value


    IF game_data.game_mode == "survival"
        IF !game_data.warmup
            IF game_data.survival_remaining
                OUTPUT 1 0 LVALUE "ingame_survival_remaining"
                OUTPUT 1 0 VALUE game_data.survival_remaining
            END IF
        END IF
    END IF

END TEMPLATE